plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'androidx.navigation.safeargs.kotlin'
}

def apiProperties = new Properties()
def apiPropertiesFile = rootProject.file("api.properties")
if (apiPropertiesFile.exists()) {
    apiProperties.load(new FileInputStream(apiPropertiesFile))
}

android {
    compileSdk 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "com.nhariza.moviesapp"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "API_KEY", "\"${System.getenv('API_KEY') ?: apiProperties['API_KEY']}\"")
    }

    sourceSets {
        androidTest {
            java.srcDirs += ['src/testBuilders/java']
        }
        test {
            java.srcDirs += ['src/testBuilders/java']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    kotlinOptions {
        useIR = true
    }

    buildFeatures {
        viewBinding true
    }

    flavorDimensions("flavorDimen")
    productFlavors {
        create("ins") {
            applicationIdSuffix = ".ins"
        }
        create("pre") {
            applicationIdSuffix = ".pre"
        }
        create("pro") {}
    }

    testOptions {
        animationsDisabled = true
    }
}

dependencies {

    implementation Kotlin.stdlib

    implementation AndroidX.coreKtx
    implementation AndroidX.appcompat
    implementation AndroidX.material
    implementation AndroidX.constraintlayout

    implementation Glide.core
    annotationProcessor Glide.compiler

    implementation Navigation.fragment
    implementation Navigation.ui

    implementation Lifecycle.viewmodel

    implementation Retrofit.core
    implementation Retrofit.gson
    implementation Retrofit.interceptor

    implementation Koin.core

    testImplementation TestLibs.arch_core
    testImplementation TestLibs.coroutines
    testImplementation TestLibs.mockk
    testImplementation TestLibs.turbine

    androidTestImplementation TestLibs.truth
    androidTestImplementation TestLibs.espresso_intents
    androidTestImplementation TestLibs.mockk_android
    androidTestImplementation TestLibs.koin
    androidTestImplementation TestLibs.navigation
    androidTestImplementation TestLibs.mockWebServer

    androidTestImplementation(TestLibs.barista) {
        exclude group: 'org.jetbrains.kotlin'
    }

    debugImplementation TestLibs.fragment

}

configurations.all {
    resolutionStrategy {
        force 'androidx.test:monitor:1.4.0'
        force 'androidx.test:core:1.4.0'
    }
}